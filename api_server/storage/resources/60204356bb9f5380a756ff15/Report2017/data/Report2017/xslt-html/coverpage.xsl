<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" version="2.0"
    xmlns:r="http://xml.di.uminho.pt/report2007"
    xmlns:p="http://xml.di.uminho.pt/paragraph2007">
    
    <xsl:template name="coverpage">
        <div class="w3-card-4">
            <xsl:apply-templates select="r:report/frontmatter"/>
            
            
            <div class="w3-container">
                <p>Lorem ipsum...</p>
            </div>
            
            <footer class="w3-container w3-blue">
                <h5>Footer</h5>
            </footer>
            
        </div>
        
        <xsl:apply-templates select="r:report/backmatter"/>
        <hr align="center" width="80%"/>
        <hr align="center" width="80%"/>
    </xsl:template>
    
    <xsl:template match="backmatter">
        <div class="footer">
            Generated by Report2007 application set created by jcr
        </div>
    </xsl:template>
    
    <xsl:template match="frontmatter/title">
        <header class="w3-container w3-light-grey w3-center">
            <h1><xsl:value-of select="."></xsl:value-of></h1>
        </header>
    </xsl:template>
    
    <xsl:template match="frontmatter/subtitle">
        <div class="w3-center">
            <h2><xsl:value-of select="."></xsl:value-of></h2>
        </div>
    </xsl:template>
    
    <xsl:template match="frontmatter/authors">
        <div class="w3-container">
            <xsl:apply-templates/>
        </div>
    </xsl:template>
    
    <xsl:template match="author">
        <h3 class="w3-center w3-margin">
            <xsl:value-of select="name"/>
        </h3>
    </xsl:template>
    
    <xsl:template match="frontmatter/dates">
        <div class="w3-container w3-margin w3-center">
            <table class="w3-table w3-border w3-card-4">
                <xsl:for-each select="date">
                    <tr>
                        <th><xsl:value-of select="value"/></th>
                        <td><xsl:value-of select="description"/></td>
                    </tr>
                </xsl:for-each>
            </table>
        </div>
    </xsl:template>
    
    <xsl:template match="frontmatter/keywords">
        <div class="w3-container w3-margin">
            <h4>Palavras-chave: </h4> 
            <p>
                <xsl:for-each select="kw">
                    <xsl:value-of select="."/>
                    <xsl:if test="following-sibling::kw">
                        <xsl:text>, </xsl:text>
                    </xsl:if>
                </xsl:for-each>
            </p>
        </div>
    </xsl:template>
    
    <xsl:template match="frontmatter/abstract">
        <div class="w3-card-4 w3-margin">
            <header class="w3-center w3-light-grey">
                <h4>Abstract</h4>
            </header>
            <xsl:apply-templates/>
        </div>
    </xsl:template>
    
    <xsl:template match="frontmatter/toc">
        <div class="w3-card-4 w3-margin">
            <a name="toc"/>
            <header class="w3-light-grey">
                <h4>Índice</h4>
            </header>
            <table class="w3-table">
                <xsl:apply-templates select="../../body" mode="toc"/>
            </table>
        </div>
    </xsl:template>
    
    <xsl:template match="chapter/title" mode="toc">
        <tr>
            <th><xsl:text>Capítulo </xsl:text>
                <xsl:number count="chapter" level="multiple"/></th>
            <td><a href="#{generate-id()}"><xsl:value-of select="."/></a></td>
        </tr>
    </xsl:template>
    
    <xsl:template match="chapter/section/title" mode="toc">
        <tr>
            <th><xsl:text>Secção </xsl:text>
                <xsl:number count="chapter|section" level="multiple"/></th>
            <td><a href="#{generate-id()}"><xsl:value-of select="."/></a></td>
        </tr>
    </xsl:template>
    
    <xsl:template match="subsection/title" mode="toc">
        <tr>
            <th><xsl:text>Subsecção </xsl:text>
                <xsl:number count="chapter|section|subsection" level="multiple"/></th>
            <td><a href="#{generate-id()}"><xsl:value-of select="."/></a></td>
        </tr>
    </xsl:template>
    
    <xsl:template match="subsubsection/title" mode="toc">
        <tr>
            <th><xsl:text>Subsubsecção </xsl:text>
                <xsl:number count="chapter|section|subsection|subsubsection" level="multiple"/></th>
            <td><a href="#{generate-id()}"><xsl:value-of select="."/></a></td>
        </tr>
    </xsl:template>
    
    <xsl:template mode="toc" match="text()" priority="-1"/>

</xsl:stylesheet>
